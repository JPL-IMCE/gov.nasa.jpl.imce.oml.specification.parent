if (name.endsWith(".tests")) {
	sourceSets {
		main {
			java.srcDirs = []
			resources.srcDirs = []
		}
		test {
			java.srcDirs = ['src', 'src-gen']
			resources.srcDirs = ['resources', 'src', 'src-gen']
			xtendOutputDir = 'xtend-gen'
		}
		all {
			resources.exclude '**/*.g', '**/*.xtext', '**/*.mwe2', '**/*.xtend', '**/*._trace'
		}
	}
	
} else if (name.endsWith(".repository")) {
	sourceSets {
		main {
			java.srcDirs = []
			resources.srcDirs = []
		}
		test {
			java.srcDirs = []
			resources.srcDirs = []
		}
	}
	
} else if (!name.endsWith(".feature") &&
		   !name.endsWith(".doc")) {
	
	sourceSets {
		main {
			java.srcDirs = ['src', 'src-gen']
			resources.srcDirs = ['src', 'src-gen']
			xtendOutputDir = 'xtend-gen'
		}
		test {
			java.srcDirs = []
			resources.srcDirs = []
		}
		all {
			resources.exclude '**/*.g', '**/*.xtext', '**/*.mwe2', '**/*.xtend', '**/*._trace'
		}
	}
	
}

plugins.withId('war') {
	webAppDirName = "WebRoot"
}

plugins.withId('org.xtext.idea-plugin') {
	assembleSandbox.metaInf.from('META-INF')
}
