apply plugin: eclipsebuild.BundlePlugin

dependencies {
	compile withEclipseBundle('com.ibm.icu')
	compile withEclipseBundle('com.google.inject')
	compile withEclipseBundle('javax.inject')
	compile withEclipseBundle('org.apache.log4j')
	compile withEclipseBundle('org.eclipse.xtext')
	compile withEclipseBundle('org.eclipse.xtext.xbase')
    compile withEclipseBundle('org.eclipse.emf.ecore.xcore')
    compile withEclipseBundle('org.eclipse.emf.ecore.xcore.lib')
	compile project(':jpl.imce.oml.specification.ecore')
    
    xtextLanguages withEclipseBundle('org.eclipse.emf.codegen.ecore')
    xtextLanguages withEclipseBundle('org.eclipse.emf.codegen.ecore.xtext')
    xtextLanguages withEclipseBundle('org.eclipse.xtext.ecore')
}
configurations {
	mwe2 {
		extendsFrom compile
	}
	xtextLanguages {
		extendsFrom compile
	}
	compile.transitive = true
	testCompile.transitive = true
}

dependencies {
   	mwe2 withEclipseBundle('org.eclipse.xtext.xtext.generator')
	mwe2 withEclipseBundle('org.eclipse.xtext.generator')
	mwe2 withEclipseBundle('org.eclipse.xpand')
	mwe2 withEclipseBundle('org.eclipse.xtend')
    mwe2 withEclipseBundle('org.eclipse.emf.mwe.utils')
    mwe2 withEclipseBundle('org.eclipse.emf.mwe2.launch')
	
	// added for Xcore support
	mwe2 withEclipseBundle('org.eclipse.emf.ecore.xcore')
    mwe2 withEclipseBundle('org.eclipse.emf.ecore.xcore.lib')
	mwe2 withEclipseBundle('org.eclipse.emf.codegen.ecore.xtext')
}

sourceSets {
	main {
		output.classesDir = 'build/target/classes/main/'
		java.srcDirs = ['src', 'src-gen', 'xtend-gen']
		resources.srcDirs = ['src', 'src-gen']
		xtendOutputDir = 'xtend-gen'
	}
    test {
        java.srcDirs = []
        resources.srcDirs = []
    }
}

task generateXtextLanguage(type: JavaExec) {
	main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
	classpath = configurations.mwe2
	inputs.file "src/jpl/imce/oml/specification/GenerateOntologicalModelingLanguage.mwe2"
	inputs.file "src/jpl/imce/oml/specification/OntologicalModelingLanguage.xtext"
	outputs.dir "src-gen"
	outputs.dir "xtend-gen"
	args += "src/jpl/imce/oml/specification/GenerateOntologicalModelingLanguage.mwe2"
	args += "-p"
	args += "rootPath=/${projectDir}/.."
}

generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]
//this is an eclipse plugin project
eclipseClasspath.enabled=false
cleanEclipseClasspath.enabled=false
